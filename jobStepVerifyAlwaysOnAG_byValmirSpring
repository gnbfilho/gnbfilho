USE msdb
GO
CREATE  OR ALTER PROCEDURE #AddStep_CheckIfAGPrimary @jobname SYSNAME
AS
BEGIN
    SET NOCOUNT ON;

    DECLARE @Job_id UNIQUEIDENTIFIER = 
    (
            SELECT [job_id]
            FROM [msdb].[dbo].[sysjobs] 
            WHERE [name] = @jobname
    )

    IF @Job_id IS NULL
    BEGIN
        
        RAISERROR('Ops, I couldn''t find the job "%s". Please ask an adult for help.',16,1, @jobname);
        RETURN;
    END

    DECLARE @Command NVARCHAR(MAX) = 
        N'IF (SELECT ars.role_desc ' + CHAR(13) + CHAR(10) +
         '   FROM sys.dm_hadr_availability_replica_states ars' + CHAR(13) + CHAR(10) +
         '   JOIN sys.availability_groups ag ON ars.group_id = ag.group_id AND ars.is_local = 1) <> ''Primary''' + CHAR(13) + CHAR(10) +
         'BEGIN ' + CHAR(13) + CHAR(10) +
         '   -- Secondary node, throw an error ' + CHAR(13) + CHAR(10) +
         '   raiserror (''Not the AG primary'', 2, 1)' + CHAR(13) + CHAR(10) +
         'END'

    DECLARE @Step_id INT
    SELECT @Step_id = [step_id]
    FROM [msdb].[dbo].[sysjobsteps]
    WHERE [job_id] = @Job_id
    AND 
        (
            [command] LIKE '%fn_hadr_is_primary_replica%'
            OR (
                    [command] like '%dm_hadr_availability_replica_states%'
                    AND [step_name] != 'Check If AG Primary'                   
                )
        )

    IF @Step_id IS NOT NULL
    BEGIN
        PRINT 'Updating job ' + @jobname
        EXEC sp_update_jobstep   
            @job_id = @Job_id   
            ,@step_id = 1
            ,@step_name = 'Check If AG Primary'
            ,@command = @command
        RETURN
    END

    IF NOT EXISTS (
            SELECT *
            FROM [msdb].[dbo].[sysjobsteps]
            WHERE [job_id] = @Job_id
                AND [step_name] = 'Check If AG Primary'
            )
    BEGIN
        PRINT 'Adding "Check If AG Primary" as first step.' + @jobName

        EXEC [msdb].[dbo].[sp_add_jobstep] @job_id = @Job_id
            ,@step_id = 1
            ,@cmdexec_success_code = 0
            ,@step_name = 'Check If AG Primary'
            ,@on_success_action = 3 -- On success, go to Next Step
            ,@on_success_step_id = 2
            ,@on_fail_action = 1 -- On failure, Quit with Success  
            ,@on_fail_step_id = 0
            ,@retry_attempts = 0
            ,@retry_interval = 0
            ,@os_run_priority = 0
            ,@subsystem = N'TSQL'
            ,@command = @command
            ,@database_name = N'master'
            ,@flags = 0

        EXEC [msdb].[dbo].[sp_update_job] @job_id = @Job_id
            ,@start_step_id = 1
    END
END
GO

EXEC #AddStep_CheckIfAGPrimary @jobname = 'bla bla bla';
